buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id "idea"
    id "java"
    id "co.riiid.gradle" version "0.4.2"
    id "com.github.spotbugs" version "4.7.1" apply false
}

repositories {
    mavenCentral()
}

idea {
    module {
        outputDir = file('build/classes/main')
        testOutputDir = file('build/classes/test')
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'jar'

test {
    useTestNG() {
        // suiteXmlFiles << new File(rootDir, "testng.xml") //This is how to add custom testng.xml
    }

    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

compileJava {
    options.compilerArgs << "-Xlint:all,-options,-static" << "-Werror"
}

dependencies {
    implementation "commons-cli:commons-cli:1.5.0"
    implementation "commons-codec:commons-codec:1.15"
    implementation "commons-io:commons-io:2.11.0"
    implementation "org.apache.httpcomponents:httpclient:4.5.13"
    implementation "org.apache.httpcomponents:httpmime:4.5.13"

    testImplementation "org.testng:testng:6.8.1"
}

jar {
    manifest {
        attributes 'Main-Class': 'com.amazonaws.awscurl.AwsCurl'
    }
    includeEmptyDirs = false
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }

    exclude 'META-INF/**'
}

jar.doLast {
    exec {
        workingDir "."
        executable "sh"
        args "-c", "cat src/main/scripts/stub.sh build/libs/awscurl.jar > build/awscurl && chmod +x build/awscurl"
        //commandLine "sh -c 'cat src/main/scripts/stub.sh build/libs/awscurl.jar > build/awscurl && chmod +x build/awscurl'"
    }
}

apply from: file("${rootProject.projectDir}/tools/gradle/java-formatter.gradle")
apply from: file("${rootProject.projectDir}/tools/gradle/check.gradle")

github {
    owner = "frankfliu"
    repo = "junkyard"
    token = "" + System.getenv("GITHUB_TOKEN")
    tagName = "0.1.0"
    targetCommitish = "master"
    name = "awscurl v0.1.0"
    body =
        """
# awscurl
## Release Notes
        """
    assets = [
            "build/libs/awscurl.jar",
            "build/awscurl"
    ]
}
