buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id "idea"
    id "java"
    id "co.riiid.gradle" version "0.4.2"
}

repositories {
    jcenter()
}

idea {
    module {
        outputDir = file('build/classes/main')
        testOutputDir = file('build/classes/test')
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'jar'

test {
    useTestNG() {
        // suiteXmlFiles << new File(rootDir, "testng.xml") //This is how to add custom testng.xml
    }

    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

compileJava {
    options.compilerArgs << "-Xlint:all,-options,-static" << "-Werror"
}

dependencies {
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.httpcomponents:httpclient:4.5.5'
    compile 'org.apache.httpcomponents:httpmime:4.5.5'

    testCompile 'org.testng:testng:6.8.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.amazonaws.awscurl.AwsCurl'
    }
    includeEmptyDirs = false
    from configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }

    exclude 'META-INF/**'
}

apply from: file("${rootProject.projectDir}/tools/gradle/formatter.gradle")
apply from: file("${rootProject.projectDir}/tools/gradle/check.gradle")


github {
    owner = "frankfliu"
    repo = "junkyard"
    token = "" + System.getenv("GITHUB_TOKEN")
    tagName = "0.1.0"
    targetCommitish = "master"
    name = "awscurl v0.1.0"
    body =
        """
# awscurl
## Release Notes
        """
    assets = [
        'build/libs/awscurl.jar'
    ]
}
