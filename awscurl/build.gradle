plugins {
    id "java"
}

dependencies {
    implementation platform("ai.djl:bom:0.23.0")
    implementation ("ai.djl.huggingface:tokenizers") {
        exclude group: "org.apache.commons", module: "commons-compress"
    }
    implementation "org.slf4j:slf4j-simple:2.0.7"
    implementation "commons-cli:commons-cli:1.5.0"
    implementation "commons-codec:commons-codec:1.15"
    implementation "commons-io:commons-io:2.11.0"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.apache.httpcomponents:httpclient:4.5.13"
    implementation "org.apache.httpcomponents:httpmime:4.5.13"

    testImplementation 'org.testng:testng:7.7.0'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.amazonaws.awscurl.AwsCurl'
    }
    includeEmptyDirs = false
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.runtimeClasspath.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude "**/*.so"
            exclude "**/*.dylib"
            exclude "**/*.dll"
        }
    }
}

jar.doLast {
    exec {
        workingDir "."
        executable "sh"
        args "-c", "cat src/main/scripts/stub.sh build/libs/awscurl*.jar > build/awscurl && chmod +x build/awscurl"
        //commandLine "sh -c 'cat src/main/scripts/stub.sh build/libs/awscurl*.jar > build/awscurl && chmod +x build/awscurl'"
    }
}
